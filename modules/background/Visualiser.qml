pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Effects
import Quickshell
import Caelestia.Components
import Caelestia.Config
import Caelestia.Services
import qs.components
import qs.services
import qs.utils

Item {
    id: root

    required property ShellScreen screen
    required property Item wallpaper

    readonly property bool shouldBeActive: Config.background.visualiser.enabled && (!Config.background.visualiser.autoHide || (Hypr.monitorFor(screen)?.activeWorkspace?.toplevels?.values.every(t => t.lastIpcObject?.floating) ?? true))
    property real offset: shouldBeActive ? 0 : screen.height * 0.2

    opacity: shouldBeActive ? 1 : 0

    Loader {
        asynchronous: true
        anchors.fill: parent
        active: root.opacity > 0 && Config.background.visualiser.blur

        sourceComponent: MultiEffect {
            source: root.wallpaper
            maskSource: wrapper
            maskEnabled: true
            blurEnabled: true
            blur: 1
            blurMax: 32
            autoPaddingEnabled: false
        }
    }

    Item {
        id: wrapper

        anchors.fill: parent
        layer.enabled: true

        Loader {
            asynchronous: true
            anchors.fill: parent
            anchors.topMargin: root.offset
            anchors.bottomMargin: -root.offset

            active: root.opacity > 0

            sourceComponent: Item {
                ServiceRef {
                    service: Audio.cava
                }

                VisualiserBars {
                    id: bars

                    readonly property string barPos: BarPosition.resolvedPosition(Config.bar.position)
                    readonly property int barZone: ShellState.componentsFor(root.screen)?.bar?.exclusiveZone ?? 0

                    anchors.fill: parent
                    anchors.leftMargin: barPos === "left" ? barZone + Tokens.spacing.small * Config.background.visualiser.spacing : Config.border.thickness
                    anchors.rightMargin: barPos === "right" ? barZone + Tokens.spacing.small * Config.background.visualiser.spacing : Config.border.thickness
                    anchors.topMargin: barPos === "top" ? barZone + Tokens.spacing.small * Config.background.visualiser.spacing : Config.border.thickness
                    anchors.bottomMargin: barPos === "bottom" ? barZone + Tokens.spacing.small * Config.background.visualiser.spacing : Config.border.thickness

                    values: Audio.cava.values
                    primaryColor: Qt.alpha(Colours.palette.m3primary, 0.7)
                    secondaryColor: Qt.alpha(Colours.palette.m3inversePrimary, 0.7)
                    rounding: Tokens.rounding.medium * Config.background.visualiser.rounding
                    spacing: Tokens.spacing.extraSmall * Config.background.visualiser.spacing
                    animationDuration: Tokens.anim.durations.normal

                    Behavior on anchors.leftMargin {
                        Anim {}
                    }

                    Behavior on anchors.rightMargin {
                        Anim {}
                    }

                    Behavior on anchors.topMargin {
                        Anim {}
                    }

                    Behavior on anchors.bottomMargin {
                        Anim {}
                    }
                }

                FrameAnimation {
                    running: root.opacity > 0 && !bars.settled
                    onTriggered: bars.advance(frameTime)
                }
            }
        }
    }

    Behavior on offset {
        Anim {}
    }

    Behavior on opacity {
        Anim {
            type: Anim.DefaultEffects
        }
    }
}
